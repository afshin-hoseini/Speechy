apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


version = "0.1.0"

android {
    compileSdkVersion 26



    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

def siteUrl = 'https://github.com/afshin-hoseini/Speechy'   // Homepage URL of the library
def gitUrl = 'https://github.com/afshin-hoseini/Speechy.git'   // Git repository URL
group = "ir.afshin.netup"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'SpeechyButton'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The MIT Software License'
                        url 'https://tldrlegal.com/license/mit-license'
                    }
                }
                developers {
                    developer {
                        id 'afshin-hoseini'
                        name 'Afshin'
                        email 'afshin.hoseini@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.android.support:cardview-v7:26.1.0'
}
repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //options.encoding = 'UTF-8'
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {

    def jsonFile = file('../Keys/keys.json')
    def jsKeys = new groovy.json.JsonSlurper().parseText(jsonFile.text)

    user = jsKeys.bintray.user
    key = jsKeys.bintray.apiKey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SpeechyButton"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}